# Datasources: https://sparql.uniprot.org/sparql/
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX glycan: <http://purl.jp/bio/12/glyco/glycan#>
PREFIX taxon: <http://purl.uniprot.org/taxonomy/>
PREFIX up: <http://purl.uniprot.org/core/>
SELECT
        DISTINCT
            ?protein ?glycosite ?glycostructure ?glycoimage
WHERE{
  ?protein up:annotation ?annotation .
  ?protein up:organism taxon:9606 .
  ?annotation a up:Catalytic_Activity_Annotation .
  ?protein up:sequence ?isoform .
  SERVICE <https://glyconnect.expasy.org/sparql> {
    ?glycosite faldo:reference ?isoform .
    ?glycosite faldo:position ?position .
    ?specificglycosite faldo:location ?glycosite .
    ?glycoprotein glycan:glycosylated_at ?specificglycosite .
    ?glycostructure glycan:glycosylates_at ?specificglycosite .
    ?glycostructure foaf:depiction ?glycoimage .
  }
}