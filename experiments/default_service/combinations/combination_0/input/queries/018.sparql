# Datasources: https://www.bgee.org/sparql/
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX genex: <http://purl.org/genex#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX orth: <http://purl.org/net/orth#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX lscr: <http://purl.org/lscr#>

SELECT ?humanProtein ?mouseProtein ?mouseOmaLink {
    {
        ?humanTaxon up:commonName 'human' .
        ?mouseTaxon up:commonName 'mouse' .
    }
    SERVICE <https://sparql.omabrowser.org/sparql> {
        ?cluster a orth:OrthologsCluster ;
            orth:hasHomologousMember ?node1 ;
            orth:hasHomologousMember ?node2 .
        ?node2 orth:hasHomologousMember* ?mouseProtein .
        ?node1 orth:hasHomologousMember* ?humanProtein .
        ?humanProtein a orth:Protein ;
            rdfs:label 'INS' ;
            orth:organism/obo:RO_0002162 ?humanTaxon .
        ?mouseProtein a orth:Protein ;
            sio:SIO_010079 ?mouseGene ; # is encoded by
            orth:organism/obo:RO_0002162 ?mouseTaxon ;
            rdfs:seeAlso ?mouseOmaLink .
        ?mouseGene lscr:xrefEnsemblGene ?mouseGeneEnsembl .
        FILTER ( ?node1 != ?node2 )
    }
    {
        ?mouseGeneBgee a orth:Gene ;
            lscr:xrefEnsemblGene ?mouseGeneEnsembl ;
            genex:isExpressedIn ?cond ;
            orth:organism/obo:RO_0002162 ?mouseTaxon .
        ?cond genex:hasAnatomicalEntity/rdfs:label 'liver' .
    }
}