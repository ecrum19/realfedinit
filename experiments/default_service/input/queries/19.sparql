# Datasources: https://sparql.orthodb.org/sparql/
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <http://purl.orthodb.org/>
PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX interpro: <http://www.ebi.ac.uk/interpro/entry/>

SELECT ?og ?equation
    (group_concat (distinct ?clade_name; SEPARATOR="; ") as ?clades)
    (count(distinct ?taxon) as ?cnt_taxons)
    (count(distinct ?gene) as ?cnt_genes)
    (group_concat (distinct ?org_name; SEPARATOR="; ") as ?taxons)
WHERE {
    ?taxon a :Species; up:scientificName ?org_name ;
        rdfs:subClassOf+/up:scientificName ?clade_name.
    ?gene a :Gene ;
        up:organism/a ?taxon.
    ?gene rdfs:seeAlso ?xref.
    ?xref a :Uniprot.
    ?gene :memberOf ?og.
    ?og :xref/:xrefResource interpro:IPR002328 ;
        :ogBuiltAt/up:scientificName "Eukaryota".

    # join via uniprot ?xref
    SERVICE <https://sparql.uniprot.org/sparql> {
        ?xref a up:Protein ;
            up:annotation/up:catalyticActivity/up:catalyzedReaction ?rh .
    }

    # join via reaction ?rh
    SERVICE <https://sparql.rhea-db.org/sparql> {
        ?rh rdfs:subClassOf rh:Reaction ;
            rh:equation ?equation.
    }

    # restrict taxons to several clades
    VALUES ?clade_name {
        "Stramenopiles"
        "Alveolata"
        "Liliopsida"
        "Coleoptera"
    }
} GROUP BY ?equation ?og ORDER BY ?og ?equation