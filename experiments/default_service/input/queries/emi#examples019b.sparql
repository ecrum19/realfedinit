# Datasources: https://biosoda.unil.ch/emi/sparql/

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX emi: <https://purl.org/emi#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX emiBox: <https://purl.org/emi/abox#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>

SELECT DISTINCT  ?wd_chem ?structure_inchikey ?parasitoidX_WD  ?parasitoidNameURI ?parasiteX_WD ?parasiteNameURI ?intxn3Label ?hostPlant_WD ?hostPlantNameURI 
WHERE {
	{SELECT DISTINCT ?parasitoidX_WD  ?parasitoidNameURI ?parasiteX_WD ?parasiteNameURI ?intxn3Label ?hostPlant_WD  ?hostPlantNameURI
	WHERE {
		{SELECT DISTINCT ?parasitoidName ?parasiteName ?parasiteX_WD ?parasitoidX_WD WHERE {
			?intxn2 emi:hasSource ?parasitoidX ;					#retrieve interaction pairs part-1
				emi:hasTarget ?parasiteX ;
				emi:isClassifiedWith ?intxnName2 .
			FILTER (REGEX(STR(?intxnName2), "RO_0002208"))				#keep interactions only if the interaction-id is RO_0002208 (parasitoidOf), e.g: 'parasitoidX' is 'parasitoidOf' 'parasiteX'
			?parasitoidX emi:inTaxon ?parasitoidX_WD ;				#retreieve wikidata-ids for parasitoidX
				     sosa:isSampleOf ?parasitoidName .				#retrieve scientific name of parasitoidX as listed in GloBI
			?parasiteX emi:inTaxon ?parasiteX_WD ;					#retreieve wikidata-ids for parasiteX
				   sosa:isSampleOf ?parasiteName .				#retreieve scientific name for parasiteX as listed in GloBI
	    	}}
		?parasiteX1 emi:inTaxon ?parasiteX_WD .						#check pasarsiteX1 is in wikidata-id parasiteX_WD (the ones obtained from interaction-pairs part-1)
		?intxn3 emi:hasSource ?parasiteX1 ; 						#retrieve interaction pairs part-2
    			emi:hasTarget ?hostPlant ;
    			emi:isClassifiedWith ?intxnName3 .
		?intxnName3 rdfs:label ?intxn3Label .
		?hostPlant emi:inTaxon ?hostPlant_WD ;						#retreieve wikidata-ids for hostPlant
			   sosa:isSampleOf ?hostPlantName .					#retreieve scientific name for hostPlant as listed in GloBI
		FILTER (!(?intxn3Label IN ("visits", "visitsFlowersOf", "pollinates")))		#keep interactions only if the interaction-names are not 'visits', 'visitsFlowersOf' or 'pollinates'. e.g.: 'parasiteX1' is 'pathogenOf/parasiteOf/..' 'hostPlant'
		SERVICE <https://qlever.cs.uni-freiburg.de/api/wikidata> {
	 		?hostPlant_WD wdt:P171* wd:Q879246 . 					#keep the interaction pairs part-2 only if hostPlant_WD has Kingdom Archaeplastida (wd:Q879246) in its lineage, this is to ensure the hostPlant is really a plant
		}
		BIND(REPLACE(STR(?parasiteName), "%20", " ") AS ?parasiteNameX)			#remove percent encodings from parasite scientific name
		BIND(IRI(?parasiteNameX) AS ?parasiteNameURI)
		BIND(REPLACE(STR(?parasitoidName), "%20", " ") AS ?parasitoidNameX)		#remove percent encodings from parasitoid scientific name
		BIND(IRI(?parasitoidNameX) AS ?parasitoidNameURI)
		BIND(REPLACE(STR(?hostPlantName), "%20", " ") AS ?hostPlantNameX)		#remove percent encodings from hostPlant scientific name
		BIND(IRI(?hostPlantNameX) AS ?hostPlantNameURI)
		
		VALUES ?hostPlant_WD { wd:Q25408 }						#filter on the host plant Achillea millefolium wikidata-id
		
	} LIMIT 1000} 
	
	{ SELECT DISTINCT ?hostPlant_WD ?wd_chem ?structure_inchikey ?npcClass WHERE {		#retrieve metabolite data
        	?material sosa:hasSample ?extract ;
                	  sosa:isSampleOf ?organe .
                ?organe emi:inTaxon ?hostPlant_WD .						#filter metabolite data which is found in wikidata-ids oh the host plant
                ?extract sosa:isFeatureOfInterestOf ?lcms .
                ?lcms sosa:hasResult ?feature_list .
                ?feature_list emi:hasLCMSFeature ?feature .
                ?feature emi:hasAnnotation ?sirius_annotation .
                ?sirius_annotation a emi:StructuralAnnotation ;
                		emi:hasChemicalStructure ?ik2d .
                ?ik2d emi:hasSMILES ?smiles ;
                      emi:isInChIKey2DOf ?structure_inchikey ;
                      emi:hasClass ?npcClass .
                ?structure_inchikey emi:isInChIKeyOf ?wd_chem .					#retrieve wikidata-ids for metabolites
		FILTER (REGEX(STR(?npcClass), "TERPEN"))					#keep if the class of metabolite is terpenes matches "TERPEN"
         }} UNION										#union with data from lotus (integrated in wikidata)
	 { SERVICE <https://qlever.cs.uni-freiburg.de/api/wikidata> {
		 ?wd_chem wdt:P235 ?structure_inchikey ;
               		  wdt:P703 ?hostPlant_WD ; 
                          ((wdt:P31|wdt:P279)/(wdt:P279*)) ?class .				#retrieve the class of the metabolite
                 VALUES ?class { wd:Q212364 wd:Q426694 }					#keep if the class of metabolite is terpenes (wd:Q212364) or terpenoids(wd:Q426694)
	 }}
}
