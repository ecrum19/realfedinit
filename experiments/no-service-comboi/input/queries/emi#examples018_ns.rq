# Datasources: sparql@https://biosoda.unil.ch/emi/sparql sparql@https://qlever.cs.uni-freiburg.de/api/wikidata
		PREFIX wd: <http://www.wikidata.org/entity/>PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX enpkg: <https://enpkg.commons-lab.org/kg/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX emi: <https://purl.org/emi#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX wgs: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX prov: <http://www.w3.org/ns/prov#>
SELECT DISTINCT ?sourceWD ?sourceNameURI ?intxnName ?targetWD ?targetNameURI WHERE {
	{
		SELECT DISTINCT ?wd_chem WHERE { #retrieve metabolite data for sourceWD
			?material sosa:hasSample ?extract ;
				sosa:isSampleOf ?organe .
			?organe emi:inTaxon ?sourceWD .
			?extract sosa:isFeatureOfInterestOf ?lcms .
			?lcms sosa:hasResult ?feature_list .
			?feature_list emi:hasLCMSFeature ?feature .
			?feature emi:hasAnnotation ?sirius_annotation .
			?sirius_annotation a emi:StructuralAnnotation ;
				emi:hasChemicalStructure ?ik2d .
			?ik2d emi:hasSMILES ?smiles ;
				emi:isInChIKey2DOf ?ik .
			?ik emi:isInChIKeyOf ?wd_chem . #retrieve wikidats-ids for metabolites
			VALUES ?wd_chem { wd:Q100138042 } #filter on wikidata-id of asimicin
		}
	}
	UNION #union with results from lotus (integrated in wikidata)
	{
			{
			?wd_chem wdt:P235 ?ik ;
				wdt:P703 ?sourceWD .
			VALUES ?wd_chem { wd:Q100138042 }
		}
	}
	?intxn emi:hasSource ?xOrg ; #retrieve interaction-pairs
		emi:hasTarget ?yOrg ;
		emi:isClassifiedWith ?intxnName .
	?xOrg emi:inTaxon ?sourceWD ; #retrieve wikidata-id of the interaction-source
		sosa:isSampleOf ?sourceName . #retrieve its scientific name as listed in GloBI
	?yOrg emi:inTaxon ?targetWD ; #retrieve wikidata-id of the interaction-target
		sosa:isSampleOf ?targetName . #retrieve its scientific names as listed in GloBI
	BIND (REPLACE(STR(?sourceName), "%20", " ") AS ?sourceNameX) #remove percent encodings from source and target names
	BIND (IRI(?sourceNameX) AS ?sourceNameURI)
	BIND (REPLACE(STR(?targetName), "%20", " ") AS ?targetNameX)
	BIND (IRI(?targetNameX) AS ?targetNameURI)
}
LIMIT 1000