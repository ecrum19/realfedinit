# Datasources: https://biosoda.unil.ch/emi/sparql https://query.wikidata.org/sparql
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX emi: <https://purl.org/emi#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
SELECT DISTINCT ?ik ?wd_id ?taxon ?taxon_name
WHERE { 
    {SELECT  ?ik ?wd_id
        WHERE {
            ?sample a emi:ExtractSample ;
                sosa:isSampleOf* ?organe ;
                sosa:isFeatureOfInterestOf ?lcms .
            ?organe emi:inTaxon ?taxon .
            ?taxon rdfs:label "melochia umbellata" .
            ?lcms a emi:LCMSAnalysisPos ;
                sosa:hasResult ?feature_list .
            ?feature_list emi:hasLCMSFeature ?feature .
            {
                ?feature emi:hasAnnotation ?annotation .
                ?annotation a emi:StructuralAnnotation ;
                    prov:wasGeneratedBy ?activity .
                ?activity prov:wasAssociatedWith <https://bio.informatik.uni-jena.de/software/sirius> .
                ?annotation emi:hasCosmicScore ?cosmic ;
                    emi:hasZodiacScore ?zodiac .
                FILTER((?cosmic > 0.5) && (?zodiac > 0.8))
            } UNION {
                ?feature emi:hasAnnotation ?annotation .
                ?annotation emi:hasTaxoScore ?taxo .
                FILTER(?taxo >= 6)
            }
            ?annotation emi:hasChemicalStructure ?ik2d .
            ?ik2d emi:hasSMILES ?smiles ;
                emi:isInChIKey2DOf ?ik .
            ?ik emi:isInChIKeyOf ?wd_id .
        } LIMIT 50
    }
        ?wd_id wdt:P31 wd:Q113145171 ;
            wdt:P703 ?taxon .
        ?taxon wdt:P225 ?taxon_name .
}