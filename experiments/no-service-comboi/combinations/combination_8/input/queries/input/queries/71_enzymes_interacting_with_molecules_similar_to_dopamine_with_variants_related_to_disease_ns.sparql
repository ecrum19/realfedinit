# Datasources: https://sparql.uniprot.org/sparql/ https://idsm.elixir-czech.cz/sparql/endpoint/chebi
PREFIX CHEBI: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX up: <http://purl.uniprot.org/core/>
SELECT
  ?protein
  ?rhea
  ?chebi
  ?disease
WHERE {
  # Use the smiles of dopamine CHEBI:18243
    ?chebi sachem:similarCompoundSearch [
      sachem:query "NCCc1ccc(O)c(O)c1" ] .
  GRAPH<https://sparql.rhea-db.org/rhea>{
    ?rhea rh:side/rh:contains/rh:compound ?compound .
    ?compound (rh:chebi|(rh:reactivePart/rh:chebi)|(rh:underlyingChebi/rh:chebi)) ?chebi .
  }
  ?protein up:reviewed true ;
    up:annotation ?caa, ?natural_variant_annotation, ?disease_annotation .
  ?caa up:catalyticActivity/up:catalyzedReaction ?rhea .
  ?natural_variant_annotation a up:Natural_Variant_Annotation ;
    skos:related ?disease .
  ?disease_annotation a up:Disease_Annotation ;
     up:disease ?disease .
}